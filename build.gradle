buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:2.0.1.RELEASE"
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.6"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'
apply plugin: 'spring-cloud-contract'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert'

group 'io.pivotal.dmfrey'
version '1.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://repo.spring.io/plugins-release" }
    maven { url "https://repo.spring.io/plugins-milestone" }
}

ext {
    springCloudVersion = 'Finchley.SR1'
    snippetsDir = file('build/generated-snippets')
}

dependencies {

    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'


    // Spring dependencies


    // Spring Boot dependencies
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-webflux')
    compile('org.springframework.boot:spring-boot-starter-security')


    // Spring Cloud dependencies
    compile('org.springframework.cloud:spring-cloud-stream-binder-rabbit')

    compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
    compile('io.pivotal.spring.cloud:spring-cloud-services-starter-service-registry')

    compile('org.springframework.cloud:spring-cloud-starter-netflix-hystrix')
    compile('io.pivotal.spring.cloud:spring-cloud-services-starter-circuit-breaker')

    compile('org.springframework.cloud:spring-cloud-starter-sleuth')

    compile('org.springframework.cloud:spring-cloud-starter-openfeign')
    compile('io.github.openfeign:feign-httpclient')


    // Third-party dependencies
    compile('com.rabbitmq:amqp-client:5.2.0')
    compile('io.vavr:vavr:0.9.2')

    annotationProcessor(
            [group: 'org.projectlombok', name: 'lombok', version: '1.16.20'],
    )
    compile('org.projectlombok:lombok:1.16.20')

    // Test dependencies
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
    testCompile('org.springframework.cloud:spring-cloud-starter-contract-verifier')
    testCompile('org.springframework.cloud:spring-cloud-starter-contract-stub-runner')

}

dependencyManagement {

    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom 'org.springframework.cloud:spring-cloud-contract-dependencies:2.0.1.RELEASE'
        mavenBom "io.pivotal.spring.cloud:spring-cloud-services-dependencies:2.0.1.RELEASE"
    }

}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn test
}

bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}
